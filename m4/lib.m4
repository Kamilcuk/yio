m4_divert(-1)
m4_include(changequote_to_tilde.m4)
m4_changecom(`/*~, `*/~)

m4_sinclude(yio_config.m4)

m4_include(lib/define_function.m4)
m4_include(lib/define_name.m4)
m4_include(lib/forloop.m4)
m4_include(lib/applyforloop.m4)
m4_include(lib/applyforloopdefine.m4)
m4_include(lib/forloopdash.m4)
m4_include(lib/applyforeach.m4)
m4_include(lib/applyforeachdefine.m4)
m4_include(lib/seq.m4)
m4_include(lib/foreach.m4)
m4_include(lib/capitalize2.m4)

/**
 * @defgroup m4 
 * @brief Group with m4 macros.
 */

/**
 * @def dnl
 * @ingroup m4
 * Define dnl just for shortcuts to m4_dnl, but please do not use it.
 */
#define dnl
m4_define(`dnl~, `m4_dnl~)

/**
 * @def m4_filename
 * @ingroup m4
 * @return The filename from __file__
 */
#define m4_filename(...)
m4_define(`m4_filename~, `m4_patsubst(m4___file__, `^.*\(/\|\\\)~)~)

/**
 * @def m4_resultname
 * @ingroup m4
 * @return The filename with m4 suffix.
 */
#define m4_resultname(...)
m4_define(`m4_resultname~, `m4_patsubst(m4_filename, `\.m4$~)~)

/**
 * @def m4_fatal_error(...)
 * @param ... List of any arguments to print
 * @ingroup m4
 * @brief Just print an error and exits m4 with an error.
 */
#define m4_fatal_error(...)
m4_define(`m4_fatal_error~, `m4_errprint(__file__:__line__`: error: $*
~)m4exit(`1~)~)

m4_define(`m4_forloopXcomma~, `m4_forloopX(`$1~, `$2~, `$3~, `,~)~)
m4_define(`m4_forloopYcomma~, `m4_forloopY(`$1~, `$2~, `$3~, `,~)~)
m4_define(`m4_forargs~, `m4_forloopXcomma(`$1~, `$2~, ``_~X~)~)
m4_define(`m4_fornumbers~, `m4_forloopXcomma(`$1~, `$2~, `X~)~)
m4_define(`m4_check_args_count~, `m4_ifelse(m4_eval(`($2) != ($3)~), `1~,
	`m4_fatal_error(`Wrong number of arguments passed to $1: $4~)~)~)

/**
 * @def m4_lib_header()
 * @ingroup m4
 * This library header to be included as the first line of a file.
 */
#define m4_lib_header(...)
m4_define(`m4_lib_header~, `// DO NOT EDIT THIS FILE!!!
// This file was autogenerated by m4___program__ 
// From m4___file__
// Edit the m4 source file to edit this file.
#if 0
#include "m4___file__"
#endif
m4_dnl Add m4_lib_header_suffix if it is defined and nonempty.
m4_ifdef(`m4_lib_header_suffix~, `m4_ifelse(m4_lib_header_suffix, `~, `~, `m4_lib_header_suffix
~)~)// #line m4___line__ "m4___file__"
m4_dnl ~)

/**
 * @def m4_config(TYPE)
 * @ingroup m4
 * @param token Type of configuration to load.
 * @brief Loads configuration from proper cfg_*.m4 file.
 */
#define m4_config(...)
m4_define(`m4_config~, `_m4_config_in(m4_ifelse(`$1~, `~, `yio~, `$1~))~)
m4_define(`_m4_config_in~, `m4_ifdef(
		`YTYPE~,
		`~,
		`// Loading cfg_$1.m4
m4_include(`cfg_$1.m4~)
m4_lib_header($1)~)~)~)

m4_divert(0)m4_dnl
